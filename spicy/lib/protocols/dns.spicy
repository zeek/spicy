# Copyright (c) 2020 by the Zeek Project. See LICENSE for details.

module DNS;

import spicy;

type RDType = enum {
    A = 1, NS = 2, MD = 3, MF = 4, CNAME = 5, SOA = 6, MB = 7, MG = 8, MR = 9,
    NULL = 10, WKS = 11, PTR = 12, HINFO = 13, MINFO = 14, MX = 15, TXT = 16,
    AAAA = 28, NBS = 32, SRV= 33, A6 = 38, EDNS = 41 };

type RRType = enum {
    ANSWER = 1, AUTH = 2, ADDL = 3
};

public type Message = unit {
    %random-access

    header:     Header;
    question:   (Question(self))[self.header.qdcount];
    answer:     (ResourceRecord(self, RRType::ANSWER))[self.header.ancount];
    authority:  (ResourceRecord(self, RRType::AUTH))[self.header.nscount];
    additional: (ResourceRecord(self, RRType::ADDL))[self.header.arcount];
};

type Header = unit {
    id    : uint16;
    flags : bitfield(16) {
            qr: 0;
            opcode: 1..4;
            aa: 5;
            tc: 6;
            rd: 7;
            ra: 8;
            z:  9..11;
            rcode: 12..15;
        } &bit-order = spicy::BitOrder::MSB0;

    qdcount: uint16;
    ancount: uint16;
    nscount: uint16;
    arcount: uint16;

    var rejected : bool;

    on %done {
        # Mimic Zeek in determining when a request has been rejected.
        if ( self.qdcount == 0 )
            self.rejected = (self.flags.rcode != 0); # 0 == NoError;

        else
            self.rejected = (self.flags.qr == 1 &&
                             self.ancount == 0 &&
                             self.nscount == 0 &&
                             self.arcount == 0);
    }
};

type Question = unit(msg: Message) {
    qname:  Name(msg);
    qtype:  uint16;
    qclass: uint16;
};

type ResourceRecord = unit(msg: Message, rrtype: RRType) {
    name:   Name(msg);
    ty:     uint16 &convert=RDType($$);
    class:  uint16;
    ttl:    uint32 &convert=cast<interval>($$);
    rdlen:  uint16;

    switch ( self.ty ) {
        RDType::NS, RDType::CNAME, RDType::PTR
                     -> rname:   Name(msg);
        RDType::A    -> a:       addr &ipv4;
        RDType::AAAA -> a:       addr &ipv6;
        RDType::MX   -> mx:      RDataMX(msg);
        RDType::SOA  -> soa:     RDataSOA(msg);
        RDType::TXT  -> txt:     (CharacterString(msg))[self.rdlen];

        *            -> rdata:   bytes &size=self.rdlen;
    };
};

type RDataMX = unit(msg: Message) {
    preference: uint16;
    name:        Name(msg);
};

type RDataSOA = unit(msg: Message) {
    mname:      Name(msg);
    rname:      Name(msg);
    serial:     uint32;
    refresh:    uint32 &convert=cast<interval>($$);
    retry:      uint32 &convert=cast<interval>($$);
    expire:     uint32 &convert=cast<interval>($$);
    minimum:    uint32 &convert=cast<interval>($$);
};

type CharacterString = unit(msg: Message) {
    len: uint8;
    data: bytes &size=(self.len);
};

type Name = unit(msg: Message) {
    : (Label(msg, self))[] &until=($$.len.offset == 0 || $$.len.compressed != 0);

    # Parsed labels are appended here.
    var label: bytes = b"";
};

type Pointer = unit(msg: Message, label: Label) {
    len: bitfield(16) {
        offset: 0..13;
        };

    name: Name(msg) &parse-at=(msg.input() + self.len.offset);
    };

type Label = unit(msg: Message, inout name: Name) {
    %random-access

    len: bitfield(8) {
        offset: 0..5;
        compressed: 6..7;
        };

    switch ( self.len.compressed ) {
        0 ->    label: bytes &size=self.len.offset {
                    name.label += b".";
                    name.label += self.label;
                    }

        3 ->    ptr: Pointer(msg, self) &parse-at=self.input() {
                    name.label += self.ptr.name.label;
                    self.adjust = 2; # Consume the additional byte in %done.
                    }
    };

    on %done {
        if ( self.adjust > 0 )
            self.set_input(self.input() + self.adjust);
    }

    var adjust: uint64 = 0;
};
